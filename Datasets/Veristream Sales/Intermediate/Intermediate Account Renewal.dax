
var _renewables = FILTER(
    NATURALINNERJOIN(
        'Staging Opportunity',
        NATURALINNERJOIN(
            'Staging Opportunity Product',
            'Staging Product'
        )
    ),
    AND(
        'Staging Opportunity'[StageName] = "Closed Won",
        'Staging Product'[Subscription__c]
    )
)

var _customers = NATURALLEFTOUTERJOIN(
    'Staging Customer',
    NATURALINNERJOIN(
        'Staging Account',
        'Staging Account Owner'
    )
)

var _won = SELECTCOLUMNS(
    NATURALINNERJOIN(
        _customers,
        _renewables
    ),
    "Customer Id", 'Staging Customer'[SaasOptics ID],
    "Account Id", 'Staging Opportunity'[AccountId],
    "Owner", 'Staging Account Owner'[Name],
    "Start Month", 'Staging Opportunity'[Start Month],
    "Renewal Month", IF(
        AND(
            MONTH([Start Month]) = 2,
            DAY([Start Month]) = 29
        ), DATE(
            YEAR([Start Month]) + IF(
                CONTAINSSTRING('Staging Product'[Name], "3 Year"), 3,
                IF(
                    CONTAINSSTRING('Staging Product'[Name], "2 Year"), 2,
                    1
                )
            ), 
            2, 
            28
        ),
        DATE(
            YEAR([Start Month]) + IF(
                CONTAINSSTRING('Staging Product'[Name], "3 Year"), 3,
                IF(
                    CONTAINSSTRING('Staging Product'[Name], "2 Year"), 2,
                    1
                )
             ),
            MONTH([Start Month]),
            DAY([Start Month])
        )
    ),
    "Has Opportunity", IF(
        CONTAINS(
            FILTER(
                'Intermediate Opportunity',
                AND(
                    NOT([Stage] IN {"Closed Lost", "Closed Won", "05 Nurture"}),
                    'Intermediate Opportunity'[Opportunity Type] = "Renewal"
                )
            ),
            'Intermediate Opportunity'[Account Id],
            'Staging Account'[Id]
        ), "Yes",
        "No"
    ),
    "Has Open Cases", IF(
        CONTAINS(
            FILTER(
                'Intermediate Case',
                'Intermediate Case'[Case Status] = "Open"
            ),
            'Intermediate Case'[Account Id],
            'Staging Account'[Id]
        ), "Yes",
        "No"
    )
)

var _renewals = GROUPBY(
    _won,
    [Account Id],
    "Owner", MAXX(CURRENTGROUP(), [Owner]),
    "Start Month", MAXX(CURRENTGROUP(), [Start Month]),
    "Renewal Month", MAXX(CURRENTGROUP(), [Renewal Month]),
    "Has Opportunity", MAXX(CURRENTGROUP(), [Has Opportunity]),
    "Has Open Cases", MAXX(CURRENTGROUP(), [Has Open Cases])
)

var _return = SELECTCOLUMNS(
    _renewals,
    "Account Id", [Account Id],
    "Owner", [Owner],
    "Start Month", [Start Month],
    "Renewal Month", [Renewal Month],
    "Has Open Cases", [Has Open Cases],
    "Has Opportunity", [Has Opportunity],
    "Days To Renewal", IF(
        DATEDIFF(
            TODAY(),
            [Renewal Month],
            DAY
        ) <= 0, 0,
        DATEDIFF(
            TODAY(),
            [Renewal Month],
            DAY
        )
    ),
    "Lost", IF(
        CONTAINS(
            FILTER(
                'Staging Opportunity',
                AND(
                    'Staging Opportunity'[Type] = "Renewal",
                    AND(
                        'Staging Opportunity'[StageName] = "Closed Lost",
                        'Staging Opportunity'[Close Month] >= [Start Month]
                    )
                )
            ),
            'Staging Opportunity'[AccountId],
            [Account Id]
        ), "Yes",
        "No"
    )
)

return SELECTCOLUMNS(
    _return,
    "Account Id", [Account Id],
    "Owner", [Owner],
    "Start Month", [Start Month],
    "Has Open Cases", [Has Open Cases],
    "Has Opportunity", [Has Opportunity],
    "Lost", [Lost],
    "Renewal Month", [Renewal Month],
    "Renewal Lead Month", DATE(
        YEAR([Renewal Month]),
        MONTH([Renewal Month]) - 1,
        1
    ) - 1,
    "Days To Renewal", [Days To Renewal],
    "Less Than 45 Days", IF(
        DATEDIFF(TODAY(), [Renewal Month], DAY) <= 45, "Less than 45",
        "Over 45"
    )
)